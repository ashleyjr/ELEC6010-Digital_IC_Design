** HSPICE file for nand3_ld
**   - generated by ext2sp v4.1

** Include netlist file for nand3_ld
.include nand3_ld.spice

** Default 3.3V Power Supply
Vsupply Vdd GND 3.3V


** 250ps input rise and fall time 
** +/- input slope of 13.2g
** Give slighlty faster output slope
** Cause ouput to change from A then B then C inputs

   VA A GND PWL(
      +  0NS         3.3V 
      +  2NS         3.3V 
      +  2.25NS      0V  
      +  5NS         0V   
      +  5.25NS      3.3V
   + )

   VB B GND PWL(
      +  0NS         3.3V  
      +  8NS         3.3V  
      +  8.25NS      0V  
      +  11NS        0V  
      +  11.25NS     3.3V
   + )

   VC C GND PWL(
      +  0NS         3.3V  
      +  14NS        3.3V  
      +  14.25NS     0V  
      +  17NS        0V  
      +  17.25NS     3.3V
   + )

.TRAN 10PS 20NS


** Measure Y output fall delays
   .measure tran fall_delay_a TRIG v(Y) VAL='3.3*0.9' TD=0NS FALL=1
   + TARG v(Y) VAL='3.3*0.1' TD=0NS FALL=1
   .measure tran fall_delay_b TRIG v(Y) VAL='3.3*0.9' TD=0NS FALL=2
   + TARG v(Y) VAL='3.3*0.1' TD=0NS FALL=2
   .measure tran fall_delay_c TRIG v(Y) VAL='3.3*0.9' TD=0NS FALL=3
   + TARG v(Y) VAL='3.3*0.1' TD=0NS FALL=3
** Take average and output
   .measure average_fall_delay PARAM = '(fall_delay_a + fall_delay_b + fall_delay_c)/3'


** Measure Y output rise delays
   .measure tran rise_delay_a TRIG v(Y) VAL='3.3*0.1' TD=0NS RISE=1
   + TARG v(Y) VAL='3.3*0.9' TD=0NS RISE=1
   .measure tran rise_delay_b TRIG v(Y) VAL='3.3*0.1' TD=0NS RISE=2
   + TARG v(Y) VAL='3.3*0.9' TD=0NS RISE=2
   .measure tran rise_delay_c TRIG v(Y) VAL='3.3*0.1' TD=0NS RISE=3
   + TARG v(Y) VAL='3.3*0.9' TD=0NS RISE=3
** Take average and output
   .measure average_rise_delay PARAM = '(rise_delay_a + rise_delay_b + rise_delay_c)/3'

** Take difference - aim for zero output
   .measure difference_rise_fall PARAM = 'average_rise_delay - average_fall_delay'


** Save results for display
.OPTIONS POST
** Avoid DC convergence in at unreasonable voltage
.OPTIONS GMINDC=1n

.END
